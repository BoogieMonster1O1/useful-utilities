plugins {
	id 'java'
	id 'scala'
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'com.matthewprenger.cursegradle' version "1.4.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "CottonMC"
		url = "https://server.bbkr.space/artifactory/libs-release"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "io.github.cottonmc:LibGui:5.1.0+1.18"
	include "io.github.cottonmc:LibGui:5.1.0+1.18"
	modImplementation("net.fabricmc:fabric-language-scala:1.1.0+scala.2.13.6") {
		exclude group: 'net.fabricmc'
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}


sourceSets {
	main {
		java {
			srcDirs = []
		}
		scala {
			srcDirs = ['src/main/scala', 'src/main/java']
		}
	}
}

tasks.withType(JavaCompile) {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	options.encoding = "UTF-8"
	options.release.set 17
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

curseforge {
	if (project.hasProperty('curse_api_key')) {
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '382093'
		changelog = 'A changelog can be found at https://github.com/BoogieMonster1O1/useful-utilities/commits'
		releaseType = 'release'
		addGameVersion '1.18'
		addGameVersion 'Fabric'
		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
			displayName = "[${project.minecraft_version}] Useful Utilities ${version}"
		}
		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}
	options {
		forgeGradleIntegration = false
	}
}
